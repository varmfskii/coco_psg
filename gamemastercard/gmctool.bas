5 REM TOOL FOR EXERCISING THE SN76489A ON THE GAME MASTER CARTRIDGE
7 REM COPIED AND MODIFIED FROM ELECTRONICS AUSTRALIA, AUGUST 1983
9 REM ARTICLE, "MAKE MUSIC WITH YOUR COMPUTER" BY PETER VERNON
10 CLS:GOSUB 900
11 X=&HFF00
12 REM SET COCO SOUND MULTIPLEXER TO CARTRIDGE INPUT
13 POKE X+1,52:POKE X+3,63
14 REM ENABLE SOUND MULTIPLEXER
15 POKE X+35,60
20 PRINT "DO YOU WANT TO"
22 PRINT TAB(15);"DEFINE FREQUENCY (1)"
24 PRINT TAB(15);"DEFINE NOISE     (2)"
26 PRINT TAB(15);"QUIT THE PROGRAM (3)"
30 PRINT TAB(15);
40 INPUT "YOUR SELECTION  ";A
50 IF A<1 OR A>3 THEN 10
60 PRINT
70 ON A GOTO 100,500,800
90 REM THIS SECTION TAKES A FREQUENCY IN HERTZ AND
92 REM RETURNS THE REGISTER VALUE REQUIRED BY THE
94 REM             SN76489A
96 REM
100 CL=4000000:REM CHANGE THIS FOR DIFFERENT CLOCK FREQUENCIES
106 CLS
110 INPUT "WHAT FREQUENCY DO YOU WANT";F0
130 R0=INT(CL/(32*F0))
140 IF R0<1 OR R0>1023 THEN GOTO 1000
150 F1=CL/(32*R0)
160 F2=CL/(32*(R0+1))
170 IF ABS(F0-F1)<ABS(F0-F2) THEN GOTO 190
180 PRINT "CLOSEST ACTUAL FREQUENCY IS";F2;"HZ"
182 R0=R0+1:PRINT "WITH REGISTER VALUE=";R0
184 GOTO 200
190 PRINT "CLOSEST ACTUAL FREQUENCY IS";F1;"HZ"
192 PRINT "WITH REGISTER VALUE=";R0
200 PRINT
210 REM THIS SECTION TAKES A REGISTER VALUE AND
220 REM A VOICE NUMBER AND RETURNS THE TWO BYTES
230 REM WHICH MUST BE SENT TO THE OUTPUT PORT TO
240 REM LOAD THAT VALUE INTO THE APPROPRIATE REGISTER
260 INPUT "VOICE TO BE USED (1 TO 3)";V
270 PRINT
280 B=INT(R0/16)
290 A=R0-B*16+128
294 A=A+32*(V-1)
300 PRINT "FOR VOICE";V
310 PRINT "FIRST  BYTE=";A
320 PRINT "SECOND BYTE=";B
350 PRINT
360 PRINT "WHAT VOLUME DO YOU WANT (15 IS LOUDEST, 0 IS OFF)";
370 INPUT VL
380 VL=15-VL
390 VB=144+VL+32*(V-1)
400 PRINT
410 PRINT "VOLUME CONTROL BYTE=";VB
420 PRINT:PRINT "ENTER 'H' TO HEAR SOUND"
430 INPUT "OR 'R' TO RETURN TO MENU";A$
440 IF A$="R" THEN GOTO 10
450 N=&HFF41
460 POKE N,A:POKE N,B
470 POKE N,VB
480 FOR D=1 TO 100:NEXT D:GOSUB 900
490 INPUT "AGAIN (Y/N)";A$
492 IF A$="Y" THEN GOTO 460 ELSE GOTO 10
500 REM THIS SECTION FORMS THE NOISE CONTROL BYTE
504 CLS
510 PRINT "SELECT WHITE NOISE (1) OR PERIODIC NOISE (0)";
520 INPUT FB
530 NB=224+FB*4
534 PRINT
540 PRINT TAB(20);"SELECT HIGH PITCH (0)"
550 PRINT TAB(20);"     MEDIUM PITCH (1)"
560 PRINT TAB(20);"        LOW PITCH (2)"
570 PRINT "PITCH UNDER CONTROL OF TONE SOURCE 3  (3)"
580 PRINT:PRINT TAB(22);:INPUT "PITCH SELECTION";NF
584 IF NF<0 OR NF>3 THEN 534
590 NB=NB+NF
600 PRINT
610 PRINT "WHAT VOLUME DO YOU WANT (15 IS LOUDEST, 0 IS OFF)";
620 INPUT V
630 PRINT
640 V=15-V
650 NV=240+V
660 PRINT "NOISE CONTROL BYTE   (SENT FIRST)=";NB
670 PRINT "VOLUME CONTROL BYTE (SENT SECOND)=";NV
680 PRINT:PRINT "ENTER 'H' TO HEAR SOUND"
690 INPUT "OR 'R' TO RETURN TO MENU";A$
700 IF A$="R" THEN GOTO 10
710 N=&HFF41
720 POKE N,NB:POKE N,NV
730 FOR D=1 TO 100:NEXT D:GOSUB 900
740 INPUT "AGAIN (Y/N)";A$
750 IF A$="Y" THEN GOTO 720 ELSE GOTO 10
800 END
900 REM THIS SECTION SHUTS UP ALL SOUND
910 N=&HFF41
920 POKE N,159:POKE N,191
930 POKE N,223:POKE N,255
940 RETURN
1000 PRINT "SORRY, FREQUENCY IS OUT OF RANGE"
1010 INPUT "PRESS 'ENTER' TO RETURN TO MENU";A
1020 GOTO 10
